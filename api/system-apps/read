#!/usr/bin/perl

#
# Copyright (C) 2018 Nethesis S.r.l.
# http://www.nethesis.it - nethserver@nethesis.it
#
# This script is part of NethServer.
#
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
#
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see COPYING.
#

use NethServer::ApiTools qw(hints success error readInput safe_decode_json invoke_info_api);
use warnings;
use strict;
use JSON;
use esmith::ConfigDB;
use File::Basename;

our $path = "/usr/share/cockpit/nethserver/applications/";
my $ret = {};
my $input = readInput();
my $cmd = $input->{'action'};

sub read_json
{
    my $file = shift;
    my $json;
    {
        local $/; #Enable 'slurp' mode
        open my $fh, "<", $file;
        $json = <$fh>;
        close $fh;
    }
    return decode_json($json);
}

sub app_info
{
    my $app = shift;
    my $shortcuts = shift;
    my $pins = shift;
    my $file = "$path$app.json";
    my $data = read_json($file);
    my $extended_data = {};

    # If available, invoke the infoapi provider
    # to extend the manifest data dynamically
    if($data->{'infoapi'}) {
        if(-x '/usr/libexec/nethserver/api/' . $data->{'infoapi'}->{'path'}) {
            $extended_data = invoke_info_api(
                '/usr/bin/sudo -n /usr/libexec/nethserver/api/' . $data->{'infoapi'}->{'path'},
                safe_decode_json($data->{'infoapi'}->{'input'}),
                $input
            );
        } else {
            warn "[ERROR] missing executable script! Actual `infoapi.path` value: " . $data->{'infoapi'}->{'path'} . "\n";
        }
    }
    $data = {%$data, %$extended_data}; # merges the input hashes

    # Set hardcoded attributes:
    $data->{'shortcut'} = defined($shortcuts->{$app}) ? 1 : 0;
    $data->{'pin'} = defined($pins->{$app}) ? 1 : 0;
    $data->{'editable'} = 1;
    return $data;
}

sub authorized_apps
{
    my $out = `/usr/libexec/nethserver/api/system-authorization/read`;
    if ($out ne '') {
        my $data = decode_json($out) || return ();
        return $data->{'applications'}
    } else {
        return ();
    }
}

if($cmd eq 'list') {

    my @apps;
    my %shortcuts;
    my %pins;
    if ($< == 0) { # non-root users can't access the config db
        my $db = esmith::ConfigDB->open_ro();

        my $shortcuts = $db->get_prop('cockpit.socket', 'Shortcuts') || '';
        foreach (split(",",$shortcuts)) {
            $shortcuts{$_} = 1;
        }

        my $pins = $db->get_prop('cockpit.socket', 'Pins') || '';
        foreach (split(",",$pins)) {
            $pins{$_} = 1;
        }
    }

    my $authorized = authorized_apps();


    foreach my $f (glob("$path*.json")) {
        my ($name,$path,$suffix) = fileparse($f,(".json"));
        if (grep(/^$name$/, @$authorized)) {
            my $data = app_info($name, \%shortcuts, \%pins) || next;
            push(@apps, $data);
        }
    }

    print encode_json(\@apps);

} elsif ($cmd eq 'launcher') {
    # Return data of specific apps without checking authorization.
    # If a list of app IDs is not provided (appIds) then data about
    # pinned apps is returned

    my @apps;
    my $appIds = $input->{'appIds'} || undef;

    if (!$appIds && $< == 0) { # non-root users can't access the config db
        my $db = esmith::ConfigDB->open_ro();

        my $pins = $db->get_prop('cockpit.socket', 'Pins') || '';
        foreach (split(",",$pins)) {
            push(@$appIds, $_);
        }
    }

    foreach my $appId (@$appIds) {
        my $file = "$path$appId.json";

        # check if app is installed (i.e. if manifest exists)
        if (-e $file) {
            my $data = app_info($appId, undef, undef) || next;
            push(@apps, $data);
        }
    }

    print encode_json(\@apps);

} elsif ($cmd eq 'info') {

    print encode_json(app_info($input->{'name'}));

} elsif ($cmd eq 'hide-uninstall') {
    my $db = esmith::ConfigDB->open_ro();
    my $hide_uninstall = $db->get_prop('cockpit.socket', 'HideUninstall') || 'disabled';
    print encode_json({ 'HideUninstall' => $hide_uninstall });
} else {
    error();
}
