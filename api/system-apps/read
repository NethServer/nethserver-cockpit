#!/usr/bin/perl

#
# Copyright (C) 2018 Nethesis S.r.l.
# http://www.nethesis.it - nethserver@nethesis.it
#
# This script is part of NethServer.
#
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
#
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see COPYING.
#

require "/usr/libexec/nethserver/api/lib/helper_functions.pl";

use warnings;
use strict;
use JSON;
use esmith::ConfigDB;
use File::Basename;

our $path = "/usr/share/cockpit/nethserver/applications/";
my $ret = {};
my $input = readInput();
my $cmd = $input->{'action'};

sub read_json
{
    my $file = shift;
    my $json;
    {
        local $/; #Enable 'slurp' mode
        open my $fh, "<", $file;
        $json = <$fh>;
        close $fh;
    }
    return decode_json($json);
}

sub app_info
{
    my $app = shift;
    my $file = "$path$app.json";
    my $data = read_json($file);
    $data->{'editable'} = is_editable();
    $data->{'shortcut'} = has_shortcut($app);
    $data->{'legacy'} = 0;
    return $data;
}

sub authorized_apps
{
    my $out = `/usr/libexec/nethserver/api/system-authorization/read`;
    if ($out ne '') {
        my $data = decode_json($out) || return ();
        return $data->{'applications'}
    } else {
        return ();
    }
}

sub is_editable
{
    return ($< == 0) ? 1 : 0; # map to integer
}

sub has_shortcut
{
    my $app = shift;
    return 0 if ($< != 0); # non-root users can't access the config db
    my $db = esmith::ConfigDB->open_ro();
    my $shortcuts = $db->get_prop('cockpit.socket', 'Shortcuts') || '';
    foreach (split(",",$shortcuts)) {
        return 1 if ($_ eq $app);
    }
    return 0;
}

sub is_installed
{
    my $app = shift;
    my $installed = shift;
    foreach (@$installed) {
        return 1 if ($app eq $_);
    }
    return 0;
}

if($cmd eq 'list') {

    my @apps;

    my $authorized = authorized_apps();

    foreach my $f (glob("$path*.json")) {
        my ($name,$path,$suffix) = fileparse($f,(".json"));
        if (grep(/^$name$/, @$authorized)) {
            my $data = app_info($name) || next;
            push(@apps, $data);
        }
    }

    if (is_editable()) {
        my @installed = `rpm -qa --queryformat "%{NAME}\n" | grep nethserver`;
        chomp @installed;
        my $legacy_apps = read_json("/etc/nethserver/cockpit/legacy-apps.json");
        foreach my $app (keys(%$legacy_apps)) {
            next if (!is_installed($app, \@installed));
            my $id = $app;
            $app =~ s/nethserver-//g;
            $app =~ s/-/ /g;
            push(@apps, {
                    "editable" => 0,
                    "shortcut" => 0,
                    "release" => { "version" => "legacy" },
                    "legacy" => 1,
                    "description" => "-",
                    "icon" => "legacy.png",
                    "name" => ucfirst($app),
                    "url" => $legacy_apps->{$id},
                    "id" => $id
                });
        }
    }

    print encode_json(\@apps);

} elsif ($cmd eq 'info') {

    print encode_json(app_info($input->{'name'}));

} else {
    error();
}
